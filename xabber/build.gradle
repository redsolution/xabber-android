apply plugin: 'com.android.application'
apply plugin: 'realm-android'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'com.frogermcs.androiddevmetrics'

android {
    compileSdkVersion 30
    buildToolsVersion '29.0.3'

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 30
        versionCode 899
        versionName '3.0.1(898)'
        vectorDrawables.useSupportLibrary = true
    }

    lintOptions {
        // problems with generated "applicationId" string resource
        disable 'MissingTranslation'
        disable 'InvalidPackage'

        abortOnError false
    }

    subprojects {
        afterEvaluate {
            if (getPlugins().hasPlugin('android') ||
                    getPlugins().hasPlugin('android-library')) {

                configure(android.lintOptions) {
                    abortOnError false

                }
            }
        }
    }

    flavorDimensions "destribution", "build"
    productFlavors {

        open {
            dimension "destribution"
            versionNameSuffix "-open"
        }

        store {
            dimension "destribution"
        }

        dev {
            dimension "build"
            applicationId "com.xabber.androiddev"
            resValue 'string', 'application_package', applicationId
            multiDexEnabled true
        }

        beta {
            dimension "build"
            applicationId "com.xabber.android.beta"
            resValue 'string', 'application_package', applicationId
            multiDexEnabled true
        }

        prod {
            dimension "build"
            applicationId "com.xabber.android"
            resValue 'string', 'application_package', applicationId
            multiDexEnabled true
        }

        vip {
            dimension "build"
            applicationId "com.xabber.androidvip"
            resValue 'string', 'application_package', applicationId
            multiDexEnabled true
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    dexOptions {
        javaMaxHeapSize "2g"
    }

    splits {
        abi {
            enable true
            reset()
            include 'armeabi', 'armeabi-v7a', 'arm64-v8a', 'mips', 'x86', 'x86_64'
            universalApk true
        }
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }

    compileOptions {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }
}

if (getGradle().getStartParameter().getTaskRequests().toString().contains("Store")) {
    apply plugin: 'com.google.gms.google-services'
    apply plugin: 'com.google.firebase.crashlytics'
    firebaseCrashlytics {
        nativeSymbolUploadEnabled true
    }
}

ext {
    smackVersion = '4.2.1-SNAPSHOT'
}

dependencies {
    implementation project(path: 'MemorizingTrustManager', configuration: 'default')
    implementation project(path: 'emojicon', configuration: 'default')

    // smack
    implementation "org.igniterealtime.smack:smack-android-extensions:$smackVersion"
    implementation "org.igniterealtime.smack:smack-experimental:$smackVersion"
    implementation "org.igniterealtime.smack:smack-tcp:$smackVersion"
    implementation "org.igniterealtime.smack:smack-resolver-dnsjava:$smackVersion"

    // android support
    implementation 'com.google.android.material:material:1.2.1'
    implementation 'androidx.legacy:legacy-support-v13:1.0.0'
    implementation 'androidx.browser:browser:1.2.0'
    implementation 'androidx.multidex:multidex:2.0.1'

    // firebase
    storeImplementation "com.google.firebase:firebase-messaging:21.0.0"
    storeImplementation 'com.google.firebase:firebase-crashlytics:17.3.0'
    storeImplementation 'com.google.firebase:firebase-analytics:18.0.0'
    storeImplementation 'com.google.firebase:firebase-crashlytics-ndk:17.3.0'

    // otr
    implementation 'com.github.redsolution:otr4j:custom-SNAPSHOT'

    // Opus codec
    implementation 'top.oply.opuslib:opuslib:1.0.2'

    // barcode scanner
    implementation 'com.journeyapps:zxing-android-embedded:3.6.0'

    // retrofit
    implementation 'com.squareup.retrofit2:retrofit:2.3.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava:2.3.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.3.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.5.0'

    // rx java
    implementation 'io.reactivex:rxandroid:1.1.0'
    implementation 'io.reactivex:rxjava:1.3.0'

    // social
    storeImplementation "com.twitter.sdk.android:twitter:3.0.0"

    // gms
    storeImplementation "com.google.android.gms:play-services-safetynet:17.0.0"
    storeImplementation "com.google.android.gms:play-services-auth:19.0.0"
    storeImplementation ("com.google.api-client:google-api-client-android:1.22.0") {
        exclude module: 'httpclient'
    }

    // okhttp
    implementation 'com.squareup.okhttp3:okhttp:3.12.1'

    // event bus
    implementation 'org.greenrobot:eventbus:3.0.0'

    // commons-io
    implementation 'commons-io:commons-io:2.6'

    // bouncycastle
    implementation group: 'org.bouncycastle', name: 'bcpkix-jdk15on', version: '1.58'

    // ui
    implementation 'me.relex:circleindicator:1.2.2@aar'
    implementation 'com.github.bumptech.glide:glide:4.9.0'
    implementation 'com.amulyakhare:com.amulyakhare.textdrawable:1.0.1'
    implementation 'ru.egslava:MaskedEditText:1.0.5'
    implementation 'eu.davidea:flexible-adapter:5.1.0'
    implementation 'eu.davidea:flexible-adapter-ui:1.0.0'
    implementation 'eu.davidea:flexible-adapter-livedata:1.0.0-b3'
    implementation 'com.soundcloud.android:android-crop:1.0.1@aar'
    api 'com.theartofdev.edmodo:android-image-cropper:2.8.0'
    implementation 'de.hdodenhof:circleimageview:3.0.1'
    implementation 'com.melnykov:floatingactionbutton:1.3.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'com.reginald:editspinner:1.1.0'

    // leak canary
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:1.5.4'
    releaseImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.4'

    // block canary
    debugImplementation 'com.github.markzhai:blockcanary-android:1.5.0'
    releaseImplementation 'com.github.markzhai:blockcanary-no-op:1.5.0'

    // test
    testImplementation 'junit:junit:4.12'
    testImplementation "org.robolectric:robolectric:4.3"
    testImplementation "org.robolectric:shadows-multidex:4.0.1"
    testImplementation "org.mockito:mockito-core:2.25.0"

    //kotlin
    implementation "androidx.core:core-ktx:1.3.2"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
}

configurations {
    all*.exclude group: 'xpp3', module: 'xpp3'
    all*.exclude group: 'com.google.guava', module:'guava-jdk5'
}
