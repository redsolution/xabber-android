apply plugin: 'com.android.application'
apply plugin: 'realm-android'
apply plugin: 'com.frogermcs.androiddevmetrics'
apply plugin: 'io.fabric'

android {
    compileSdkVersion 28
    buildToolsVersion '28.0.3'

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 28
        versionCode 531
        versionName '2.4.2(531)'
    }

    lintOptions {
        // problems with generated "applicationId" string resource
        disable 'MissingTranslation'
        disable 'InvalidPackage'

        abortOnError false
    }

    subprojects {
        afterEvaluate {
            if (getPlugins().hasPlugin('android') ||
                    getPlugins().hasPlugin('android-library')) {

                configure(android.lintOptions) {
                    abortOnError false

                }
            }
        }
    }

    flavorDimensions "build"
    productFlavors {

        dev {
            dimension "build"
            applicationId "com.xabber.androiddev"
            resValue 'string', 'application_package', applicationId
            multiDexEnabled true
        }

        beta {
            dimension "build"
            applicationId "com.xabber.android.beta"
            resValue 'string', 'application_package', applicationId
            multiDexEnabled true
        }

        prod {
            dimension "build"
            applicationId "com.xabber.android"
            resValue 'string', 'application_package', applicationId
            multiDexEnabled true
        }

        vip {
            dimension "build"
            applicationId "com.xabber.androidvip"
            resValue 'string', 'application_package', applicationId
            multiDexEnabled true
        }

        ru {
            dimension "build"
            applicationId "com.xabber.android.ru"
            resValue 'string', 'application_package', applicationId
            multiDexEnabled true
        }

    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'),
                    'proguard-rules.pro'
            // enable crashlytics
            buildConfigField "boolean", "USE_CRASHLYTICS", "true"
        }
        debug {
            // disable crashlytics
            buildConfigField "boolean", "USE_CRASHLYTICS", "false"
        }
    }

    dexOptions {
        javaMaxHeapSize "2g"
    }

    splits {
        abi {
            enable true
            reset()
            include 'armeabi', 'armeabi-v7a', 'arm64-v8a', 'mips', 'x86', 'x86_64'

            universalApk true
        }
    }

    afterEvaluate {
        initFabricPropertiesIfNeeded()
    }
}

def initFabricPropertiesIfNeeded() {
    def propertiesFile = file('fabric.properties')

    def apiSecret = hasProperty('crashlyticsApisecret') ? crashlyticsApisecret : System.getenv('crashlyticsApisecret')
    def apiKey = hasProperty('crashlyticsApikey') ? crashlyticsApikey : System.getenv('crashlyticsApikey')

    if (!propertiesFile.exists()) {
        def commentMessage = "This is autogenerated fabric property from system environment to prevent key to be committed to source control."
        ant.propertyfile(file: "fabric.properties", comment: commentMessage) {
            entry(key: "apiSecret", value: apiSecret)
            entry(key: "apiKey", value: apiKey)
        }
    }
}

def build_param = "${build}";

if (build_param == "dev") {
    //exclude all but dev
    android.variantFilter { variant ->
        if (!variant.getFlavors().get(0).name.equals('dev')) {
            variant.setIgnore(true);
        }
    }
}

ext {
    smackVersion = '4.2.1-SNAPSHOT'
    supportVersion = '28.0.0'
}

dependencies {
    implementation project(path: 'MemorizingTrustManager', configuration: 'default')
    implementation project(path: 'emojicon', configuration: 'default')

    implementation 'com.github.str4tocaster.smack:smack-android-extensions:ffb99cc'
    implementation 'com.github.str4tocaster.smack:smack-experimental:ffb99cc'
    implementation 'com.github.str4tocaster.smack:smack-tcp:ffb99cc'
    implementation 'com.github.str4tocaster.smack:smack-resolver-dnsjava:ffb99cc'

    implementation "com.android.support:design:$supportVersion"
    implementation "com.android.support:support-v13:$supportVersion"
    implementation "com.android.support:customtabs:$supportVersion"
    implementation 'com.android.support:multidex:1.0.3'

    // firebase
    compile 'com.google.firebase:firebase-messaging:11.4.0'

    implementation 'com.github.Str4tocaster:otr4j:custom-SNAPSHOT'
    implementation 'com.google.zxing:android-integration:3.3.0'

    // retrofit
    implementation 'com.squareup.retrofit2:retrofit:2.3.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava:2.3.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.3.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.5.0'

    // rx java
    implementation 'io.reactivex:rxandroid:1.1.0'
    implementation 'io.reactivex:rxjava:1.1.0'

    // social
    implementation 'com.google.android.gms:play-services-safetynet:11.4.0'
    implementation 'com.facebook.android:facebook-android-sdk:4.31.0'
    implementation 'com.twitter.sdk.android:twitter:3.0.0'
    implementation 'com.google.android.gms:play-services-auth:11.4.0'
    implementation ('com.google.api-client:google-api-client-android:1.22.0') {
        exclude module: 'httpclient'
    }

    implementation 'me.relex:circleindicator:1.2.2@aar'
    implementation 'com.github.bumptech.glide:glide:3.7.0'
    implementation 'com.squareup.okhttp3:okhttp:3.5.0'
    implementation 'org.greenrobot:eventbus:3.0.0'
    implementation 'commons-io:commons-io:2.6'

    // ui
    implementation 'com.amulyakhare:com.amulyakhare.textdrawable:1.0.1'
    implementation 'ru.egslava:MaskedEditText:1.0.5'
    implementation 'eu.davidea:flexible-adapter:5.0.0-rc4'
    implementation 'com.soundcloud.android:android-crop:1.0.1@aar'
    implementation 'de.hdodenhof:circleimageview:2.2.0'
    implementation 'com.melnykov:floatingactionbutton:1.3.0'

    implementation('com.crashlytics.sdk.android:crashlytics:2.9.5@aar') {
        transitive = true
    }

    implementation group: 'org.bouncycastle', name: 'bcpkix-jdk15on', version: '1.58'

    // leak canary
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:1.5.4'
    releaseImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.4'

    // block canary
    debugImplementation 'com.github.markzhai:blockcanary-android:1.5.0'
    releaseCompile 'com.github.markzhai:blockcanary-no-op:1.5.0'

}
apply plugin: 'com.google.gms.google-services'

configurations {
    all*.exclude group: 'xpp3', module: 'xpp3'
}
